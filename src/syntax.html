<h2>A lil' bit of context</h2>
<p>
    To help you understand ASM, let's first talk about what it is. Basically, ASM is raw CPU instructions. You're literally writing directives for the CPU.
    <br />
    ASM is not <em>a</em> language, but a class of languages; rather, there is one ASM language per CPU in existence. The reason is that, for example, GBz80 ASM is the ASM language understood by the GB's CPU, the GBz80. Similarly, x86 ASM is the ASM understood by the x86 family of CPUs.
</p>
<p>
    ASM really feels like a list, in a way. Here's a sample of ASM, to give you a preview:
</p>
<pre>
    cp a, $11 ; Compare value left by boot ROM
    jr nz, .notGBC
    ld a, 1
    jr .gotConsoleType

.notGBC
    xor a

.gotConsoleType
    ldh [hConsoleType], a ; Store console type for later checking (in FFFE, to avoid clearing)

    xor a
    ldh [rLCDC], a ; Shut LCD down

    ; Clear HRAM, except console type
    ld c, $FD
.clearHRAM
    ld [$ff00+c], a
    dec c
    bit 7, c
    jr nz, .clearHRAM</pre>
<p>
    If you're familiar with C, this snippet is more or less this:
</p>
<pre>
    hConsoleType = a == 0x11 ? 1 : 0;

    a = 0;
    LCDC = a;

    c = 0xFD;
    do {
        *c = a;
        c--;
    } while(c &amp; 0x80);</pre>
<p>
    We'll see the point of this snippet in a moment. For now, we're going to use it as an example of how ASM is written, so its purpose is irrelevant (though it will be explained later).
</p>

<h2>A line of ASM</h2>
<p>
    To explain how ASM is written, we'll analyze one line of the lines of the snippet presented above: <code>cp a, $11 ; Compare value left by boot ROM</code>.
    <br />
    This line is split in two by the half-colon (<code>;</code>). To its left is the <em>instruction</em>; to its right is the <em>comment</em>.
</p>
<p>
    A comment can be anything, and is ignored by the compiler (and thus aren't included in the final ROM): the point is to provide context to some code. Comments are optional, and can also be alone on their line.
</p>